plugins {
    id "com.android.application"
}

android {
    namespace "com.mrboomdev.scrollix"
    compileSdk 34

    defaultConfig {
        applicationId "com.mrboomdev.scrollix"
        minSdk 24
        targetSdk 34

        var timezone = TimeZone.getTimeZone("UTC")
        var calendarCurrent = Calendar.getInstance(timezone)
        var calendarStart = Calendar.getInstance(timezone)
        calendarStart.set(2024, 0, 0)

        // Only a single valid version can be made once a day,
        // so please do not release a new update on the same day with a previous one!
        var newVersionCodeTimeout = 1000 * 60 * 60 * 24

        versionName calendarCurrent.format("YY.D.Hmmss")
        versionCode (((calendarCurrent.timeInMillis / newVersionCodeTimeout) - (calendarStart.timeInMillis / newVersionCodeTimeout)) as int)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BUILD_DATE_YEAR", "\"" + calendarCurrent.get(Calendar.YEAR) + "\"")
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions = [ "engine" ]
    productFlavors {
        gecko {
            dimension "engine"
            applicationIdSuffix ".gecko"
            versionNameSuffix "-gecko"
        }

        chromium {
            dimension "engine"
            applicationIdSuffix ".chromium"
            versionNameSuffix "-chromium"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.10.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.core:core-splashscreen:1.0.1"

    implementation "com.squareup.moshi:moshi:1.15.0"
    implementation "com.squareup.okhttp3:okhttp:4.9.2"

    implementation "com.nostra13.universalimageloader:universal-image-loader:1.9.5"
    implementation "jp.wasabeef:recyclerview-animators:4.0.2"

    geckoImplementation "org.mozilla.geckoview:geckoview-nightly:121.0.20231112094805"
    chromiumImplementation "androidx.webkit:webkit:1.9.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}